
PROJECT( CORE_magick )

# Include coders source files
INCLUDE(coders.cmake)

FILE( GLOB ${LIBRARY_TARGET_NAME}_PRIVATE_HDRS *private.h )
FILE( GLOB ${LIBRARY_TARGET_NAME}_PUBLIC_HDRS *.h )
FILE( GLOB ${LIBRARY_TARGET_NAME}_SRCS *.c )
LIST( REMOVE_ITEM ${LIBRARY_TARGET_NAME}_PUBLIC_HDRS ${${LIBRARY_TARGET_NAME}_PRIVATE_HDRS} )

IF( ${CMAKE_SYSTEM_NAME} STREQUAL "Linux" AND ${CMAKE_SYSTEM_PROCESSOR} STREQUAL "x86_64" )
	SET_SOURCE_FILES_PROPERTIES(${${LIBRARY_TARGET_NAME}_SRCS} ${CODERS_SRCS}
               PROPERTIES COMPILE_FLAGS "-fPIC")
ENDIF( ${CMAKE_SYSTEM_NAME} STREQUAL "Linux" AND ${CMAKE_SYSTEM_PROCESSOR} STREQUAL "x86_64" )

STRING( TOUPPER ${LIBRARY_TARGET_NAME} UPPERCASE_LIBRARY_TARGET_NAME )
SET( LIBRARY_CONFIG_FILE ${CMAKE_CURRENT_BINARY_DIR}/${LIBRARY_TARGET_NAME}-config.cmake )

SET( IMAGEMAGICK_CONFIG_PREFIX MAGICKCORE_ )
SET( HASHCMAKEDEFINE \#cmakedefine )
INCLUDE( ../ConfigurePrefix.cmake )
SET( MAGICK_CONFIG_HEADER_FILE ${CMAKE_CURRENT_BINARY_DIR}/magick-config.h )
CONFIGURE_FILE( ${CMAKE_CURRENT_SOURCE_DIR}/../config/config.h.cmake
	${MAGICK_CONFIG_HEADER_FILE}.step1 @ONLY )
CONFIGURE_FILE( ${MAGICK_CONFIG_HEADER_FILE}.step1 ${MAGICK_CONFIG_HEADER_FILE} )

INCLUDE_DIRECTORIES( ${CMAKE_CURRENT_BINARY_DIR}/.. ${CMAKE_CURRENT_SOURCE_DIR}/.. )
ADD_LIBRARY( ${LIBRARY_TARGET_NAME} ${LIBRARY_BUILD_TYPE} 
	${${LIBRARY_TARGET_NAME}_SRCS} 
	${MAGICK_CONFIG_HEADER_FILE}
	${${LIBRARY_TARGET_NAME}_PRIVATE_HDRS} 
	${${LIBRARY_TARGET_NAME}_PUBLIC_HDRS} 
	${${LIBRARY_TARGET_NAME}_WIN32_XTRAS} 
	${CODERS_SRCS} )
TARGET_LINK_LIBRARIES( ${LIBRARY_TARGET_NAME} ${CODERS_DEPENDENT_LIBRARIES} )

# Install targets
IF( WIN32 AND NOT ${IMAGEMAGICK_NAMESPACE_NAME}_BUILD_STATIC_LIB )
	SET_TARGET_PROPERTIES( ${LIBRARY_TARGET_NAME} PROPERTIES IMPORT_SUFFIX _dll.lib )
	INSTALL( TARGETS ${LIBRARY_TARGET_NAME} ARCHIVE
		DESTINATION ${LIB_INSTALL_DIR} )
ENDIF( WIN32 AND NOT ${IMAGEMAGICK_NAMESPACE_NAME}_BUILD_STATIC_LIB )


INSTALL( TARGETS ${LIBRARY_TARGET_NAME} EXPORT ${IMAGEMAGICK_NAMESPACE_NAME}-targets ${LIBRARY_INSTALL_TYPE}
	 DESTINATION ${LIB_INSTALL_DIR} )
INSTALL( FILES ${${LIBRARY_TARGET_NAME}_PUBLIC_HDRS} ${MAGICK_CONFIG_HEADER_FILE}
	DESTINATION ${INC_INSTALL_DIR}/magick )

